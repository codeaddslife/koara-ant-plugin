plugins {
    id 'net.saliman.cobertura' version '2.3.1'
    id 'com.github.kt3k.coveralls' version '2.6.3'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'net.saliman.cobertura'
apply plugin: 'com.github.kt3k.coveralls'

group = 'io.koara.ant'
jar.baseName = 'koara-ant-plugin'
version= '0.4.0'

ext.koaraVersion = '0.15.0'

repositories {
	mavenCentral()
}

cobertura.coverageFormats = ['xml']

compileJava {
	sourceCompatibility = 1.6
	targetCompatibility = 1.6
}

signing {
	required = false
	sign configurations.archives
}

task sourcesJar(type: Jar, dependsOn:classes) {
	classifier = 'sources'
	from sourceSets.main.allJava.srcDirs
}

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

artifacts {
	archives jar
	archives javadocJar
	archives sourcesJar
	archives shadowJar
}

shadowJar {
  exclude 'org/'
  exclude 'images/'
  exclude 'META-INF/'
}

dependencies {
	compile 'org.apache.ant:ant:1.9.6'
	compile "com.codeaddslife.koara:koara:${koaraVersion}"
	compile "com.codeaddslife.koara:koara-html:${koaraVersion}"
	compile "com.codeaddslife.koara:koara-xml:${koaraVersion}"
	testCompile 'junit:junit:4.12'
	testCompile 'org.slf4j:slf4j-nop:1.7.21'
}

uploadArchives {
	repositories {
		mavenDeployer {
			beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
			repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
				ext.sonatypeUsername = rootProject.hasProperty('sonatypeUsername') ? rootProject.sonatypeUsername : ''
				ext.sonatypePassword = rootProject.hasProperty('sonatypePassword') ? rootProject.sonatypePassword : ''
				authentication(userName: sonatypeUsername, password: sonatypePassword)
			}

			pom.project {
				name 'koara'
				packaging 'jar'
				description 'Koara converter task for Ant'
				url 'http://www.koara.io'

				scm {
					url 'https://github.com/codeaddslife/koara-ant-plugin'
					connection 'https://github.com/codeaddslife/koara-ant-plugin'
					developerConnection 'https://github.com/codeaddslife/koara-ant-plugin'
				}
				licenses {
					license {
						name 'The Apache Software License, Version 2.0'
						url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
						distribution 'repo'
					}
				}
				developers {
					developer {
						id 'codeaddslife'
						name 'Andy Van Den Heuvel'
					}
				}
			}
		}
	}
}